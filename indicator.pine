// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/

//@version=5
indicator("Buy/Sell Pressure Volume Bars", format=format.volume)

// User inputs
showMetric = input.bool(true, "Show Volume Metric", tooltip="Toggle to show or hide the volume metric.")
showHighVolume = input.bool(true, "Show High Volume", tooltip="Toggle to highlight high volume bars (+).")
showExtremeVolume = input.bool(true, "Show Extreme Volume", tooltip="Toggle to highlight extreme volume bars (!).")
metricDisplay = input.string("Line", title="Volume Metric Display", options=['Line', 'Bars'], tooltip="Choose how the volume metric is displayed: as a line or as bars.")
volumeMetric = input.string("Baseline", title="Volume Metric", options=['SMA', 'Baseline'], tooltip="Select the type of volume metric: Simple Moving Average (SMA) or the proprietary Baseline.")
volumeMetricLength = input.int(50, minval=10, maxval=500, title="Volume Metric Length", tooltip="Set the length for the volume metric calculation.")
volumeMedianSessionData = input.string("Extended", title="Volume Baseline Session Data", options=['Regular', 'Extended'], tooltip="Choose the session data type for calculating the volume baseline: Regular or Extended.")
showRegularMedian = input.timeframe("30", title="Only Show Regular Session Data Above", tooltip="The timeframe above which only regular session data is displayed in the Baseline, even if extended session data is selected.")
futuresRegularSession = input("0830-1500:23456", title="Futures Regular Session", tooltip="Define the regular trading session hours for futures.")
HVMultiplier = input(2.0, title="High Volume Multiplier", tooltip="Multiplier for high volume (volume > 2x metric).")
EVMultiplier = input(3.0, title="Extreme Volume Multiplier", tooltip="Multiplier for extreme volume (volume > 3x metric).")
bullColor = input(color.rgb(8, 153, 129), title="Buy Volume Color")
bearColor = input(color.rgb(242, 54, 69), title="Sell Volume Color")
metricColor = input(color.rgb(83, 83, 83, 30), title="Volume Metric Bars Color")
averageLineColor = input(color.rgb(243, 152, 15), title="Volume Metric Line Color")
highVolumeColor = input(color.white, title="High Volume Color")

// Volume calculations
gapUp = close[1] < low ? low - close[1] : 0
gapDown = close[1] > high ? close[1] - high : 0
buyVolume = volume * (close - (low - gapUp)) / ((high + gapDown) - (low - gapUp))
sellVolume = volume
usingSMA = volumeMetric == "SMA"
usingLine = metricDisplay == "Line"
volumeSMA = usingSMA ? ta.sma(volume, volumeMetricLength) : 0
medianVolume = 0.0

if not na(volume)
    // Volume median calculation
    if showMetric and not usingSMA
        var float[] regularMarketVolume = array.new_float(volumeMetricLength)
        var float[] extendedMarketVolume = array.new_float(volumeMetricLength)

        var bool isFuture = str.endswith(syminfo.ticker, "!") and syminfo.type == "futures"
        bool isFutureRegularSession = not na(time(timeframe.period, futuresRegularSession))
        var bool useRegular = (timeframe.in_seconds(showRegularMedian) / 60) >= (timeframe.in_seconds() / 60)
        
        if volumeMedianSessionData == "Extended" and (session.ispremarket or session.ispostmarket or (isFuture and (isFutureRegularSession or timeframe.isdwm))) and useRegular
            if extendedMarketVolume.size() == volumeMetricLength
                extendedMarketVolume.shift()
            array.push(extendedMarketVolume, volume)
        else if (session.ismarket and not isFuture) or (isFuture and (isFutureRegularSession or timeframe.isdwm))
            if regularMarketVolume.size() == volumeMetricLength
                regularMarketVolume.shift()
            array.push(regularMarketVolume, volume)

        if volumeMedianSessionData == "Regular"
            medianVolume := array.median(regularMarketVolume)
        else if volumeMedianSessionData == "Extended"
            if (session.ismarket and not isFuture) or (isFuture and (isFutureRegularSession or timeframe.isdwm)) or not useRegular
                medianVolume := array.median(regularMarketVolume)
            else
                medianVolume := array.median(extendedMarketVolume)
else
    buyVolume := na
    sellVolume := na
    showMetric := false

// Calculate the volume metric based on user selection
volumeMetricValue = usingSMA ? volumeSMA : medianVolume

// Calculate the threshold for high volume based on standard deviation
float highVolumeThreshold = HVMultiplier * volumeMetricValue
float extremeVolumeThreshold = EVMultiplier * volumeMetricValue

// Plot volume metric
plot(showMetric and not usingLine ? volumeMetricValue : na, style=plot.style_columns, color=metricColor, title="Volume Metric as Bars")

// Plot buy and sell volumes
plot(sellVolume, style=plot.style_columns, color=bearColor, title="Sell Volume")
plot(buyVolume, style=plot.style_columns, color=bullColor, title="Buy Volume")

// Plot volume metric line
plot(showMetric and usingLine ? volumeMetricValue : na, style=plot.style_line, color=averageLineColor, title="Volume Metric as Line")

// Check for extreme volume and plot exclamation mark
highVolumeCondition = session.ismarket and showHighVolume ? volume > highVolumeThreshold : na
extremeVolumeCondition = session.ismarket and showExtremeVolume ? volume > extremeVolumeThreshold : na
plotchar(showMetric and highVolumeCondition and not extremeVolumeCondition, title="High Volume Indicator", char='+', location=location.top, color=highVolumeColor, size=size.tiny)
plotchar(showMetric and extremeVolumeCondition, title="Extreme Volume Indicator", char='!', location=location.top, color=highVolumeColor, size=size.tiny)